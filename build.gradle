buildscript {
    repositories {
        jcenter()
        maven { 
			url = "http://files.minecraftforge.net/maven"
		}
		maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.10"
    }
}

repositories {
    jcenter()
		
	maven { 
		name = "jitpack"
		url "https://jitpack.io" 
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven'

version = "1.12.2.${buildNum}"
group = 'com.rabbit'
archivesBaseName = "rabbit-gui"


sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.3.2678"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
	
	replaceIn "RabbitGui.java"
	replace "@VERSION", project.version
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade 'net.objecthunter:exp4j:0.4.5'
}

jar {	
	
    from (sourceSets.main.output) {
        setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    }
    
    configurations.shade.each{ dep ->
        from(project.zipTree(dep)){
            exclude 'META-ING', 'META-INF/**', '**/LICENSE.txt'
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

curseforge {
  apiKey = "${api_key}"
  project {
    id = '294047'
    changelog = 'consult the github'
    releaseType = 'release'
  }
}

task incrementVersion {
    ant.propertyfile(file: "gradle.properties") {
        entry( key: "buildNum", type:"int", default:"0", operation:"+")
    }
}

//enforce correct order
tasks.build.mustRunAfter clean

//curseforge uses these tasks
tasks.curseforge.dependsOn clean, build, incrementVersion