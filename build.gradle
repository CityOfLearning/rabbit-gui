// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "v1.3.0"
group= "com.rabbit"
archivesBaseName = "rabbit-gui"

minecraft {
    version = "1.8.9-11.15.1.1855"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_20"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade 'net.objecthunter:exp4j:0.4.5'
}

task shadedJar(type: Jar){
    from (sourceSets.main.output) {
        setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    }
    configurations.shade.each{ dep ->
        from(project.zipTree(dep)){
            exclude 'META-ING', 'META-INF/**'
        }
    }
    classifier = 'shaded'
}

task sourceJar(type: Jar) { 
    from sourceSets.main.allSource
    classifier = 'sources'
}

def action = new Action<net.minecraftforge.gradle.tasks.user.reobf.ArtifactSpec>(){
    void execute(net.minecraftforge.gradle.tasks.user.reobf.ArtifactSpec artifactSpec) {
        artifactSpec.classpath = sourceSets.main.compileClasspath
    }
}

reobf.reobf(shadedJar, action)

tasks.jar.dependsOn('sourceJar', 'shadedJar')

artifacts {
    archives jar
    archives sourceJar
    archives shadedJar
}


compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}


processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
